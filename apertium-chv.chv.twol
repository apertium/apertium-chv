Alphabet
  а ӑ е ё ӗ и о у ӳ ы э ю я б в г д ж з к л м н п р с ҫ т ф х ц ч ш щ й ь ъ
  А Ӑ Е Ё Ӗ И О У Ӳ Ы Э Ю Я Б В Г Д Ж З К Л М Н П Р С Ҫ Т Ф Х Ц Ч Ш Щ Й Ь Ъ 
 %{A%}:а %{A%}:е %{A%}:я
 %{Ă%}:ӑ %{Ă%}:ӗ %{Ă%}:0
 %{Ĕ%}:ӑ %{Ĕ%}:ӗ !%{Ĕ%}:0
 %{ă%}:ӑ %{ă%}:ӗ %{ă%}:0
 %{T%}:т %{T%}:р %{T%}:ч
 %{и%}:и %{и%}:ӗ %{и%}:0 
 %{м%}:м %{м%}:0
 %{н%}:н %{н%}:0
 %{N%}:н %{N%}:0
 %{й%}:й %{й%}:0 %{й%}:ь 
 %{U%}:ӳ %{U%}:у
 %{ː%}:0 %{ː%}:н
 %{в%}:в %{в%}:0
 %{ъ%}:0 
 %{ь%}:0
 %{ԁ%}:0 %{ԁ%}:ь
 %{а%}:а %{а%}:ӑ
 %{е%}:е %{е%}:ӗ
 %{о%}:о %{о%}:ӑ

 %-
 %{☭%}:0
 %>:0 
;

Sets

ArchiBack = %{ъ%} ; 

ArchiFront = %{ь%} ; 

BackVow = ӑ а ы о у я ё ю ;

FrontVow = ӗ э и ӳ е ; 

UnderBack = %{о%} %{а%} ; 

UnderFront = %{е%} ; 

YotisedVow = е я ю ;

ForceHarmony = %{ъ%} %{ь%} ;

Vow = ӑ а ы о у я ё ю ӗ э и ӳ е ;

ArchiCns = %{м%} %{н%} %{T%} %{й%} %{ː%} %{N%} %{в%} ;

Cns = б в г д ж з к л м н п р с ҫ т ф х ц ч ш щ й ; 

Rules 

"Dative {N} not realised after consonant"
%{N%}:0 <=> [ й: | :Cns | %{й%}: ] [ :0 ]* %>: _ ;

!@ и ю н ь:0 >:0 {N}:0 {A}:е

!@ й ӗ п {ː}:п >:0 {N}:0 {A}:е

!@ м е н ю {в}:0 {☭}:0 >:0 {N}:н {A}:е

!@ п р о с п е к т {ъ}:0 >:0 {N}:0 {A}:а

!@ з в е н о >:0 {N}:н {A}:а

"Soft sign deletes in some contexts"
ь:0 <=> _ [ :0 ]* %>: %{N%}: ;             ! Dative
        _ [ :0 ]* %>: %{и%}:ӗ ;
    д:ч _ %>: ;
        _ %{й%}:й ; 
        _ %>: %{Ă%}: н ; ! genitive
        _ %>: %{ă%}: м ; ! px1sg
        _ %>: %{Ĕ%}: р ; ! px2pl
        _ %>: %{U%}:  ; ! px2sg

!@ к у к ӑ л ь:0 >:0 {N}:0 {A}:е

!@ и ю н ь:0 >:0 {и}:ӗ {н}:0 >:0 {Ă}:0 н

!@ т е т р а д ь:0 >:0 {ă}:ӗ м

"Yot deletes before following yotised consonant"
й:0 <=> _ %>: [ %{U%}:ю | %{A%}:я ] ;

!@ у й:0 >:0 {U}:ю {н}:0

!@ т у х ь {й}:0 я:0 >:0 {U}:ю {н}:0

!@ н я н {й}:0 я:0 >:0 {U}:ю {н}:0

!@ а л л е {й}:0 я:0 >:0 {U}:ю {н}:0

"Underlying yot deletion before dative {N}"
й:0 <=> _ [ :0 - %{ː%}: ]* %>: %{N%}: ;

!@ у й:0 >:0 {N}:0 {A}:я

!@ в ӑ й {ː}:й ӑ:0 >:0 {N}:0 {A}:а

"Back vowel harmony for yotised {A}"
%{A%}:я <=> [ UnderBack: | BackVow ] [ :Cns | :0 ]* й:0 %>: [ :Cns | :0 ]*  _ ; 
        except 
                                                          _ %>: %{и%}: %{н%}: %>: %{N%}: %{A%}: ; ! ger

!@ ч е й:0 >:0 {N}:0 {A}:е

"Back vowel harmony for unyotised {A}"
%{A%}:а <=> [ UnderBack: | BackVow ] [ :Cns | :0 ]* %>: [ :Cns | :0 ]*  _ ; 
                                         [ ArchiBack: | UnderBack: ] %>: [ :Cns | :0 ]*  _ ; 
              :BackVow [ :Cns | :0 ]* %>: [ :Cns | :0 ]* _ ;
      except 
          [ UnderFront: | FrontVow ] [ :Cns | [ :0 - ArchiBack: - UnderBack: ] ]* %>: [ :Cns | :0 ]*  _ ; 
                                   [ д | н | л ] ь: %>: [ :Cns | :0 ]*  _ ;
                                                й:0 %>: [ :Cns | :0 ]*  _ ; 
                                              %{и%}: [ :Cns | :0 ]* _ ;
                                                          _ %>: %{и%}: %{н%}: %>: %{N%}: %{A%}: ; ! ger

!@ б а р о к к {о}:ӑ >:0 {N}:н {A}:а

!@ ш о с с е >:0 {N}:н {A}:е

!@ п р о с п е к т {ъ}:0 >:0 {N}:0 {A}:а

!@ с к л а д >:0 {U}:у {н}:н >:0 {T}:т {A}:а

!@ в у л а >:0 н {A}:0 >:0 {и}:и {н}:н >:0 {N}:0 {A}:е

"Archiphoneme {A} deletes in certain verb forms with possessive"
%{A%}:0 <=> _ %>: %{и%}: %{н%}: %>: %{N%}: %{A%}: ; 

"Back vowel harmony for {Ă}"
%{Ă%}:ӑ <=> [ UnderBack: | BackVow ] [ :Cns | :0 ]* %>: [ :Cns | :0 ]*  _ ; 
                        [ ArchiBack: | UnderBack: ] %>: [ :Cns | :0 ]*  _ ; 
                            :BackVow [ :Cns | :0 ]* %>: [ :Cns | :0 ]*  _ ; 
                            :BackVow [ :Cns | :0 ]* _ ; 
      except 
          [ UnderFront: | FrontVow ] [ :Cns | [ :0 - ArchiBack: - UnderBack: ] ]* %>: [ :Cns | :0 ]*  _ ; 
                                   [ д | н | л ] ь: %>: [ :Cns | :0 ]*  _ ;
                                                й:0 %>: [ :Cns | :0 ]*  _ ; 
                                              ! [ %{и%}: | %{U%}: ] [ :Cns | :0 ]* _ ;
                                               %{и%}: [ :Cns | :0 ]* _ ;
                                               %{U%}: [ :Cns | :0 ]* %>: _ н ; ! genitive after px2sg takes front
                                                      Vow [ :0 ]* %>: _ н ; ! genitive, deletes
!@ м и н и с т е р с т в {а}:0 >:0 {Ă}:ӑ н

!@ ҫ ы н {ː}:н >:0 {Ĕ}:ӑ р >:0 ш {Ă}:ӑ н

!@ х ӗ в >:0 {U}:ӳ {н}:0 >:0 ш {Ă}:ӗ н

!@ х ӑ {й}:й ю:ӑ {в}:в >:0 {U}:у {н}:0 >:0 ш {Ă}:ӑ н

!@ у й:0 >:0 {U}:ю {н}:0 >:0 ш {Ă}:ӑ н

"Back vowel harmony for {Ĕ}"
%{Ĕ%}:ӑ <=> [ UnderBack: | BackVow ] [ :Cns | :0 ]* %>: [ :Cns | :0 ]*  _ ;
                        [ ArchiBack: | UnderBack: ] %>: [ :Cns | :0 ]*  _ ;
        except
          [ UnderFront: | FrontVow: ] [ :Cns | [ :0 - ArchiBack: - UnderBack: ] ]* %>: [ :Cns | :0 ]*  _ ; 
                                   [ д | н | л ] ь: %>: [ :Cns | :0 ]*  _ ;
                                                      :Vow [ :0 ]* %>: _  ;

!@ ш о с с е:0 >:0 {Ĕ}:ӗ р

!@ з в е н о:0 >:0 {Ĕ}:ӑ р

"Back vowel harmony for yotised {U}"
%{U%}:ю <=> [ UnderBack: | BackVow ] [ :Cns | :0 ]* й:0 %>: [ :Cns | :0 ]*  _ ; 
            [ UnderBack: | BackVow ] [ :Cns | :0 ]* й: %>: _ ; 
                        [ ArchiBack: | UnderBack: | BackVow: ] й: %>: _ ; 
        except
                                                      :Vow [ :0 - й: ]* %>: _  ;
                                               ArchiFront: [ :0 ]* %>: _  ;

!@ а н н {ь}:0 >:0 {U}:ӳ {н}:0

!@ а л л е {й}:0 я:ю >:0 {U}:0 {н}:0

!@ г е р о й:0 >:0 {U}:ю {н}:0 >:0 с е {м}:м

"Back vowel harmony for unyotised {U}"
%{U%}:у <=> [ UnderBack: | BackVow ] [ :Cns | :0 ]* %>: [ :Cns | :0 ]*  _ ; 
                        [ ArchiBack: | UnderBack: ] %>: [ :Cns | :0 ]*  _ ; 
       except
          [ UnderFront: | FrontVow ] [ :Cns | [ :0 - ArchiBack: - UnderBack: ] ]* %>: [ :Cns | :0 ]*  _ ; 
                                   [ д | н | л ] ь: %>: [ :Cns | :0 ]*  _ ;
                                                й: %>: [ :Cns | :0 ]*  _ ; 
                                               :Vow ( :0 )* %>: _  ;
                                               ArchiFront: [ :0 ]* %>: _  ;
!@ а н н {ь}:0 >:0 {U}:ӳ {н}:0

"Vowel assimilation before {U}"
Vx:Vy <=> _ [ :0 - й: ]* %>: %{U%}:  ;
     except
         %{й%}: _ %{в%}: %>: %{U%}:  ;  
                _ [ :0 ]* %>: %{Ĕ%}: р ;
                _ %>: %{и%}: ;
                _ %{й%}: %>: %{U%}: ;
                _ %{й%}: %>: %{Ĕ%}: ;
                и: _ %{в%}: %{☭%}: %>: %{U%}: ;
      where Vx in ( а э и о я е ы ё %{е%} %{о%} %{а%} )
            Vy in ( у ӳ ӳ у ю ӳ у у   ӳ     у     у ) 
      matched ;

!@ у ч и л и щ {е}:ӳ >:0 {U}:0 {н}:0

!@ х ӑ {й}:й ю:ӑ {в}:в >:0 {U}:у {н}:0

!¬ З о л о т н и ц к и {й}:0 >:0 {U}:0 {н}:0 >:0 с е {м}:м >:0 п {A}:е

"Vowel reduction before {Ĕ}"
Vx:Vy <=> _ [ :0 ]* %>: %{Ĕ%}: р ;
      except 
          _ %>: %{и%}: ;
          _ %{й%}: %>: %{Ĕ%}: ;
                и: _ %{в%}: %{☭%}: %>: %{U%}: ;
      where Vx in ( а э и о у я е ы ё ю  ) 
            Vy in ( ӑ ӗ ӗ ӑ ӑ ӑ ӗ ӑ ӑ ӑ  ) 
      matched ;

"Vowel deletion for possessive"
Vx:0 <=> :Vow [ :0 ]* %>: _  ;
       except 
         [ UnderBack: | BackVow ] [ :Cns | :0 ]* й:0 %>: [ :Cns | :0 ]*  _ ;
            [ UnderBack: | BackVow ] [ :Cns | :0 ]* й: %>: _ ;
                        [ ArchiBack: | UnderBack: | BackVow: ] й: %>: _ ;
                                              и %{й%}: %>: _ ; 
       where Vx in ( %{Ĕ%} %{U%} %{ă%} ) ;

!@ ӗ ҫ >:0 {N}:0 {A}:е

!@ з в е н о >:0 {ă}:0 м

!@ к и н о {в}:0 {☭}:0 >:0 {ă}:0 м

"Back vowel harmony for {ă}"
%{ă%}:ӑ <=> [ UnderBack: | BackVow ] [ :Cns | :0 ]* %>: [ :Cns | :0 ]*  _ ;
                        [ ArchiBack: | UnderBack: ] %>: [ :Cns | :0 ]*  _ ;
        except
          [ UnderFront: | FrontVow ] [ :Cns | [ :0 - ArchiBack: - UnderBack: ] ]* %>: [ :Cns | :0 ]*  _ ; 
                                                                 [ д | н | л ] ь: %>: [ :Cns | :0 ]*  _ ;
                                                                                      Vow [ :0 ]* %>: _ м ;
                                                                                     :Vow [ :0 ]* %>: _  ;

"Vowel deletion for {Ă} in genitive"
%{Ă%}:0 <=> [ Vow | %{и%}: | %{U%}: ] [ :0 ]* %>: _ н ;

!@ р а д и о {в}:0 {☭}:0 >:0 {Ă}:0 н

!@ б а р о к к {о}:0 >:0 {и}:и {н}:0 >:0 {Ă}:0 н

!@ п у р т {ː}:т ӑ:у >:0 {U}:0 {н}:0 >:0 {Ă}:0 н

!@ х у р ҫ ӑ:0 >:0 {U}:у {н}:0 >:0 {Ă}:0 н

"Consonant gemination"
%{ː%}:Cx <=> Cx _ [ :0 ]* %>: ;
             Cx _ :Vow %>: ;
      except
            _ %>: [ %{T%}: | р | п | ш ] ; ! locative, ins, ter
            _ :Vow %>: [ %{T%}: | р | п | ш ] ; ! locative, ins, ter
            _ (:Vow) %>: с ;              ! plural
      where Cx in Cns  ;

!@ у т {ː}:т ӑ:0 >:0 {N}:0 {A}:а

!@ ч и к {ː}:к ӗ:0 >:0 {Ĕ}:ӗ р

!@ с ӑ в {ː}:в ӑ >:0 {Ĕ}:0 р

"Weak stem vowel deletion in dative/genitive"
Vx:0 <=> _ %>: %{N%}: %{A%}:  ; ! dative
         _ %>: %{Ă%}: ;  ! genitive
         _ %>: %{ă%}: ;  ! px1sg
         _ %>: %{U%}: ;  ! px2sg
      where Vx in ( ӑ ӗ ) ;

!@ п у л {ː}:л ӑ:0 >:0 {N}:0 {A}:а

!@ п у л {ː}:л ӑ:0 >:0 {ă}:ӑ м

"Stem vowel deletion before possessive"
Vx:0 <=> _ %>: %{и%}: ; ! possessive
         _ %>: %{Ă%}: ; ! genitive
         _ %>: %{ă%}: ; ! poss. 1sg
         _ %>: %{Ĕ%}: ;  ! px2pl
      except
         _ [ :0 - й: ]* %>: %{U%}: ; 
      where Vx in ( %{а%} %{о%} %{е%} )  ;

"Stem vowel reduction before suffix"
Vx:Vy <=> _ %>: [ %{N%}: | %{T%}: | р | п | ш ] ; ! dat,loc,abl,ins,ter
          _ %>: с ;  ! plural
      except 
          _ [ :0 - й: ]* %>: %{U%}: ; ! px2sg
      where Vx in ( %{а%} %{о%} %{е%} ) 
            Vy in (   ӑ     ӑ     ӗ )
      matched ;

!! к о ф {е} >:0 {N}:н {A}

!! л и т е р а т у р {а}:ӑ > {N} {A}

!@ с т е н а:а >:0 {N}:н {A}:а

"Consonant shortening before suffix"
Cx:0 <=> Cx _ [ :0 ]* %>: [ %{N%}: | %{T%}: | р | п | ш ] ; ! dat, loc, abl, ins, ter, dat
         Cx _ %>: %{и%}:  ; ! Possessive
         Cx _ %>: с ; ! Plural
         Cx _ %>: %{Ă%}: н ; ! genitive
         Cx _ %>: %{ă%}:  ; ! possessive
         Cx _ %>: %{Ĕ%}:  ; ! possessive
         Cx _ %>: %{U%}:  ; ! possessive
         Cx _ %>: н %{A%}:  ; ! optional dative
      except
             .#. а ш _ %>: ;  ! Irregular form of атте
!                 Cx  _ (ь: ) [ :0 ]* %>: %{и%}: ; ! palatalisation
            _ %>: [ %{U%}:ю | %{A%}:я ] ;
          й:  _ [ :0 - %{ː%}: ]* %>: %{N%}:  ;  ! класс>{N}{A}--класа 
      where Cx in Cns  ;

!@ к л а с с:0 >:0 {N}:0 {A}:а

!@ к л а с с:0 >:0 н {A}:а

!@ м е т а л л:0 >:0 {и}:ӗ {н}:0 >:0 ш {Ă}:ӗ н

!@ м е т а л л:0 >:0 {N}:0 {A}:а

"Loanwords do not take {в} before some suffixes"
%{в%}:0 <=> _ %{☭%}: %>: [ %{N%}: | %{T%}: | р | с | ш | п ] ; 
            _ %{☭%}: %>: %{Ă%}: ; ! gen
            _ %{☭%}: %>: %{ă%}: ; ! px1sg
            _ %{☭%}: %>: %{Ĕ%}: ; ! px2pl
            _ %{☭%}: %>: %{U%}: ; ! px2sg
            _ %{☭%}: .#. ; 

!@ к и н о {в}:0 {☭}:0 >:0 {N}:н {A}:а

"{в} doesn't surface before some suffixes"
%{в%}:0 <=> _ %>: [ %{T%}: %{A%}: | р %{A%}: | ш %{Ă%}: ] ;  ! Locative, abl, ter
            _ ([ :0 - %{☭%}: ]) %>: с ;  ! Plural
            _ ([ :0 - %{☭%}: ]) %>: п ; ! ins
            _ ([ :0 - %{☭%}: ]) .#. ; 

!@ т у {в}:0 >:0 {T}:р {A}:а

"Vowel reduction for high vowels before {в}"
Vx:Vy <=> _ %{в%}:в %>: [ %{и%}: %{н%}: | %{N%}: %{A%}: | н %{A%}: ] ; 
          _ %{в%}:в %{☭%}: %>: %{и%}: %{н%}: ; 
          _ %{в%}:в %>: %{Ă%}: н ;  ! genitive
          _ %{в%}:в %>: %{ă%}: м ;  ! px1sg
          _ %{в%}:в %>: %{Ĕ%}: р ;  ! px2pl
          _ %{в%}:в %>: %{U%}: ;  ! px2sg
       where Vx in ( у ӳ ) 
             Vy in ( ӑ ӗ ) 
       matched ; 

!@ х ӑ {й}:й ю:ӑ {в}:в >:0 {ă}:ӑ м

!@ п ӳ:ӗ {в}:в >:0 {N}:0 {A}:е

"Forbid two soft signs in a row"
%{й%}:ь /<= ь _ ; 

!@ т у х ь {й}:й я:0 >:0 {N}:0 {A}:я

"Soft sign turns to i kratka before reduced vowel" 
ь:й <=> _ [ :ӗ | :ӑ ] %>:  ;
        _ [ UnderBack: | UnderFront: | Vow:0 ] %>: %{и%}:и ; 
        _ [ UnderBack: | UnderFront: | Vow:0 ] %>: %{Ă%}:ӗ ; 
        _ [ UnderBack: | UnderFront: | Vow:0 ] %>: %{Ĕ%}:ӗ ; 
        _ [ е:ӳ |  %{е%}:ӳ ] %>: %{U%}: ; ! px2sg

!@ п л а т ь:й {е}:ӗ >:0 {Ĕ}:0 р

!@ м е н {й}:ь ю:ӑ {в}:0 {☭}:0 >:0 {Ĕ}:0 р
        
!@ п л а т ь:й {е}:ӗ >:0 {Ă}:ӑ н

!@ п л а т ь:й {е}:ӗ >:0 {N}:н {A}:е

!@ а н с а м б л ь >:0 {и}:ӗ {н}:н >:0 {T}:ч {A}:е

!@ к ӗ с ь:й е:0 >:0 {и}:и {н}:н >:0 {N}:0 {A}:е

"No yots before yotised vowels and string final"
%{й%}:0 <=> Vow _ %>: [ :Cns | :0 ]* :YotisedVow ;  
              н _ ( :0) %>: %{и%}: ; 
          Cns ь _ :YotisedVow ;
                _ :YotisedVow (%{в%}: ) %>: ; 
                _ ( :YotisedVow) [ :0 ]* .#. ;
                _ %>: %{Ă%}: н ; ! genitive
                _ %>: ш  ; ! ter, ins
                _ :YotisedVow [ :0 ]* %>:  ;
                _ :YotisedVow %{в%}: ; ! менювӗ
       except 
                _ %>: %{U%}:ӳ ; 
                и _ %>: %{Ĕ%}: ; 

!@ м е н {й}:0 ю {в}:0 {☭}:0 >:0 п {A}:а

!@ ш ӑ ш и {й}:й >:0 {U}:ӳ {н}:0

!@ и н ф о р м а ц и {й}:0 >:0 {N}:0 {A}:е

!@ н я н {й}:0 я:0 >:0 {и}:и {н}:н >:0 {N}:0 {A}:е

!@ т у х ь {й}:0 я >:0 {T}:р {A}:а

!@ н я н {й}:ь я:ӑ >:0 с е {м}:м
 
"Forbid i kratka following н"
%{й%}:ь <=> н _  [ я:ӑ | ю:ӑ ] ; ! \:YotisedVow ; 

!@ н я н {й}:ь я:ӑ >:0 {N}:н {A}:а

"Vowel reduction for yotised consonants"
Vx:ӑ <=> %{й%}: _ (%{в%}: ) %>: %{N%}: %{A%}: ;  ! dative
         %{й%}: _ %{в%}: %>: %{и%}:  ; ! possessive 
         %{й%}: _  %>: с  ;  ! plural
         %{й%}: _  %>: п  ;  ! instrumental 
         %{й%}: _  %>: %{T%}:  ;  ! locative
         %{й%}: _  %>: ш  ;  ! term.
         %{й%}: _ (%{в%}: ) %>: %{Ă%}:  ;  ! genitive
         %{й%}: _ (%{в%}: ) %>: %{ă%}:  ;  ! px1sg
         %{й%}: _ %{в%}: %>: %{U%}:  ;  ! px2sg
         %{й%}: _ %{в%}: %>: %{Ĕ%}:  ;  ! px2pl
     except 
         _ [ :0 - й: ]* %>: %{U%}: ; ! px2sg yotised
         _ [ :0 ]* %>: %{Ĕ%}: р ; ! px yotised
     where Vx in ( я ю ) ; 

!@ с т и х и {й}:й я:ӑ >:0 {T}:р {A}:а н

!@ х ӑ {й}:й ю:ӑ {в}:в >:0 {ă}:ӑ м

!@ х ӑ {й}:й ю:ӑ {в}:в >:0 {Ă}:ӑ н

!@ х ӑ {й}:0 ю:ӑ {в}:в >:0 {и}:0 {н}:н >:0 {N}:0 {A}:е

!@ а л л е {й}:й я:ӑ >:0 {N}:н {A}:а

!@ в л а ҫ >:0 {N}:0 {A}:а

!@ т у х ь {й}: я >:0 {N}:н {A}:а

!@ х ӑ {й}:й ю:ӑ {в}:в >:0 {Ĕ}:ӑ р >:0 с е {м}:м >:0 ш {Ă}:ӗ н

"Possessive {и} does not appear after consonants in dative"
%{и%}:0 <=> :Cns [ :0 - [ UnderBack: | UnderFront: | Vow:0 ] ]* %>: _ ; 
                                                         :Cns ь %>: _ ; 
                                                       и %{й%}: %>: _ ;
        except
                                                     _ %{н%}: %>: %{T%}: ; 
                                          %{ː%}: %>: _ ;
                                                     _ %{н%}: %>: [ ш | п ] ; ! term.,ins
                                                     _ %{н%}: %>: %{Ă%}: ; ! gen.
                                                     _ %{н%}: %>: с ; ! plur.
                                                     _ %{н%}: .#. ; ! nom.
                                   %{в%}: %{☭%}: %>: _ ;
                                                     %{A%}: %>: _ %{н%}: %>: %{N%}: %{A%}: ; ! ger

!@ и ю л ь >:0 {и}:0 {н}:н >:0 {N}:0 {A}:е

!@ р а д и о {в}:в {☭}:0 >:0 {и}:ӗ {н}:н >:0 {N}:0 {A}:е

!@ а в т а н >:0 {и}:ӗ {н}:0 >:0 ш {Ă}:ӗ н

!@ у р а м >:0 {и}:ӗ {н}:0 >:0 п {A}:е

!@ в у л а >:0 н {A}:0 >:0 {и}:и {н}:н >:0 {N}:0 {A}:е

"Possessive {и} appears after deleted stem vowels"
%{и%}:и <=> [ UnderBack: | UnderFront: | Vow:0 | %{A%}:0 ] %>: _ ; 
                                          %{ː%}: %>: _ ;

!@ й ӗ п {ː}:п >:0 {и}:и {н}:н >:0 {N}:0 {A}:е

!@ в у л а >:0 н {A}:0 >:0 {и}:и {н}:н >:0 {N}:0 {A}:е

"Possessive deletes full stem vowels"
Vx:0 <=> _ %>: %{и%}: ; 
!         _ %>: %{Ĕ%}: ; 
     where Vx in ( е о а ӑ ӗ я ) ;

!@ к ӗ н е к е:0 >:0 {и}:и {н}:н >:0 {N}:0 {A}:е

"Possessive {н} surfaces after consonants"
%{н%}:н <=> [ :Cns | Cns ь ] [ :0 ]* %>: %{и%}: _ ; 
                            и %{й%}: %>: %{и%}: _ ;
                                     %>: %{U%}: _ %>: %{T%}: ;
        except
            _ %>: [ ш | п ] ; ! term./ins.
            _ %>: %{Ă%}: н ; ! gen
            _ %>: с ; ! pl.
            _ .#. ; 

!@ и н ф о р м а ц и {й}:0 >:0 {и}:0 {н}:н >:0 {N}:0 {A}:е

!@ ш ӑ ш и {й}:0 >:0 {и}:0 {н}:н >:0 {N}:0 {A}:е

!@ ш о с с е:0 >:0 {и}:и {н}:0 >:0 ш {Ă}:ӗ н

!@ ӗ ҫ >:0 {и}:ӗ {н}:0 >:0 {Ă}:0 н
 
"Palatalisation of {T} after {н}"
Cx:ч <=> [ %{н%}:н | %{м%}:н ]  %>: _ ; 
     except 
            %{U%}: %{н%}: %>: _ ; 
     where Cx in ( р %{T%} ) ;

"{T} after liquids and nasals"
%{T%}:т <=> [ р | н ] [ ь: | :0 - %{ː%}:0 ]* %>: _ ; 
                              %{U%}: %{н%}:н %>: _ ;
            л (ь: ) %>: _ ;  

!@ х ӗ в е л >:0 {T}:т {A}:е н

"Forbid double р across morpheme boundary"
р:р /<= _ %>: р ; 

!¬ к о н т р о л ё р >:0 р {A}:а 

!@ к л а с с:0 >:0 {и}:ӗ {н}:н >:0 {T}:ч {A}:е

"Palatalisation of stem final -/t/ in possessive"
Cx:ч <=> \Cx _ (ь: ) [ :0 ]* %>: %{и%}:ӗ ;        ! doesn't apply when duplicated
     where Cx in ( т д ) ; 

"Realisation of plural {м}"
%{м%}:м <=> _ .#. ;           ! Nominative
            _ %>: [ п | ш ] ; ! Instrumental, Terminative

"Realisation of plural {м} as oblique"
%{м%}:н <=> _ %>: %{T%}: ;

!@ ч ӗ р ӗ п >:0 с е {м}:н >:0 {T}:ч {A}:е

"Orthographic detail for и with иу to ю"
и:0 <=> _ о: %{в%}: %{☭%}: %>: %{U%}: ; 

"Orthographic detail for о with иу to ю"
о:ю <=> и: _ %{в%}: %{☭%}: %>: %{U%}: ; 

!@ р а д и:0 о:ю {в}:0 {☭}:0 >:0 {U}:0 {н}:0

!@ р а д и о {в}:в {☭}:0 >:0 {и}:ӗ {н}:0 >:0 с е {м}:0 >:0 {Ă}:0 н

"Orthographic detail soft sign in back vowel verbs"
%{ԁ%}:ь <=> :BackVow :т _ ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!	тухья<n><dat>	тухь{й}я>{N}{A}	тухьяна	тухьйӑна	тухьяна
!!	тухья<n><px3sp><dat>	тухь{й}я>{и}{н}>{N}{A}	тухйине	тухьйине	тухйине
!!	утӑ<n><px3sp><dat>	ут{ː}ӑ>{и}{н}>{N}{A}	утине	уттине	утине
!!	аллея<n><loc>	алле{й}я>{T}{A}	аллейӑра	аллеяра	аллейӑра
!!	няня<n><loc>	нян{й}я>{T}{A}	няньӑра	няняра	няньӑра
!!	стихия<n><loc>	стихи{й}я>{T}{A}	стихийӑра	стихияра	стихийӑра
!!	утӑ<n><px3sp><loc>	ут{ː}ӑ>{и}{н}>{T}{A}	утинче	уттинче	утинче
!!	информаци<n><px3sp><loc>	информаци{й}>{и}{н}>{T}{A}	информацинче	информацийӗнче	информацинче
!!	стихия<n><pl><nom>	стихи{й}я>се{м}	стихисем	стихийӑсем	стихисем
!!	тухья<n><pl><nom>	тухь{й}я>се{м}	тухьясем	тухйӑсем	тухьясем
!!	тухья<n><ins>	тухь{й}я>п{A}	тухьяпа	тухйӑпа	тухьяпа
!!	музей<n><gen>	музей>{Ă}н	музейӑн	музейӗн	музейӑн
!!	тухья<n><gen>	тухь{й}я>{Ă}н	тухьян	тухйӑн	тухьян
!!	тухья<n><ter>	тухь{й}я>ш{Ă}н	тухьяшӑн	тухйӑшӑн	тухьяшӑн
!!	утӑ<n><px3sp><ter>	ут{ː}ӑ>{и}{н}>ш{Ă}н	утишӗн	уттишӗн	утишӗн
!!	платье<n><px1sg><nom>	плать{е}>{ă}м	платьем	платьӗм	платьем
!!	тухья<n><px1sg><nom>	тухь{й}я>{ă}м	тухьям	тухйӑм	тухьям
!!	стихия<n><px2pl><nom>	стихи{й}я>{Ĕ}р	стихир	стихийӑр	стихир
!!	хӑю<n><px2pl><nom>	хӑ{й}ю{в}>{Ĕ}р	хӑйӑвӑр	хӑювӑр	хӑйӑвӑр

