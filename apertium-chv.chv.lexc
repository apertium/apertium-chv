!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Multichar_Symbols

%<n%>                                ! Имя существительное
%<np%>                               !

%<pl%>                               ! Множественное число

%<px1sg%>                            ! 
%<px2sg%>                            ! 
%<px3sp%>                            ! 
%<px1pl%>                            ! 
%<px2pl%>                            ! 

%<nom%>                              ! Именительный падеж
%<dat%>                              ! Дательный падеж
%<gen%>                              ! Родительный падеж
%<loc%>                              ! Местный падеж
%<abl%>                              ! Исходный падеж
%<ins%>                              ! Творительный падеж
%<ter%>                              ! 

%<apos%>                             !
%<cm%>                               ! 
%<guio%>                             ! 
%<lpar%>                             ! 
%<lquot%>                            ! 
%<rquot%>                            ! 
%<sent%>                             ! 


%{Ă%}                                ! ӑ / ӗ / 0
%{Ĕ%}                                ! ӑ / ӗ / 0
%{A%}                                ! а / е / 0
%{н%}                                ! н / 0
%{й%}                                ! й / 0
%{и%}                                ! и / ӗ / 0
%{м%}                                ! м / н 
%{T%}                                ! т / р / ч
%{N%}                                ! н / 0
%{ă%}                                ! ӑ / ӗ / 0
%{U%}                                ! у / ӳ / 0
%{в%}                                ! в / 0
%{а%}                                ! а / ӑ / 0
%{о%}                                ! о / ӑ / 0
%{е%}                                ! е / ӗ / 0

%{ː%}                                ! 
%{ъ%}                                ! 
%{ь%}                                !
%{☭%}                                ! 

%>                                   ! Граница морфем

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

Nouns ;
Proper ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON K

# ; 

LEXICON CASE 

%<nom%>:                             K ;
%<gen%>:%>%{Ă%}н                     K ;
%<dat%>:%>%{N%}%{A%}                 K ; 
%<loc%>:%>%{T%}%{A%}                 K ; 
%<abl%>:%>%{T%}%{A%}н                K ; 
%<ins%>:%>п%{A%}                     K ;
%<ter%>:%>ш%{Ă%}н                    K ; 

LEXICON PLURAL

                                     CASE ;
%<pl%>:%>се%{м%}                     CASE ; 

LEXICON POSS 

                                     PLURAL ;
%<px1sg%>:%>%{ă%}м                   PLURAL ;
%<px2sg%>:%>%{U%}%{н%}               PLURAL ;
%<px3sp%>:%>%{и%}%{н%}               PLURAL ;
%<px1pl%>:%>%{ă%}м%{Ă%}р             PLURAL ;
%<px2pl%>:%>%{Ĕ%}р                   PLURAL ;

LEXICON SUBST 

POSS ; 

LEXICON NP

%<np%>:                              SUBST ;

LEXICON N

%<n%>:                               SUBST ;

LEXICON POSS-ANNE 

:нне                                 PLURAL ;
%<px1sg%>:нн%>ем                     PLURAL ;
%<px2sg%>:нн%{ь%}%>%{U%}%{н%}        PLURAL ;
%<px3sp%>:мӑш%>%{и%}%{н%}            PLURAL ;
%<px1pl%>:нне%>м%{Ă%}р               PLURAL ;
%<px2pl%>:нн%>ӗр                     PLURAL ;

LEXICON POSS-ATTE 

:тте                                 PLURAL ;
%<px1sg%>:тт%>ем                     PLURAL ;
%<px2sg%>:ҫ%>%{U%}%{н%}              PLURAL ;
%<px3sp%>:шш%>%{и%}%{н%}             PLURAL ;
%<px1pl%>:тте%>м%{Ă%}р               PLURAL ;
%<px2pl%>:ҫ%>ӑр                      PLURAL ;

LEXICON POSS-ATTEANNE

:тте-анне                            PLURAL ;
%<px1sg%>:тте-анн%>ем                PLURAL ;
%<px2sg%>:ҫу-анн%{ь%}%>%{U%}%{н%}    PLURAL ;
%<px3sp%>:шӗ-амӑш%>%{и%}%{н%}        PLURAL ;
%<px1pl%>:тте-анне%>м%{Ă%}р          PLURAL ;
%<px2pl%>:ҫӑр-анн%>ӗр                PLURAL ;

LEXICON N-ANNE 

%<n%>:                               POSS-ANNE ; 

LEXICON N-ATTE 

%<n%>:                               POSS-ATTE ; 

LEXICON N-ATTEANNE

%<n%>:                               POSS-ATTEANNE ; 

LEXICON N-VLAÇ

%<n%>:ҫ                              SUBST ;
%<n%>%<nom%>:сть                     # "weight: 1.0" ; 
%<n%>%<px2sg%>%<dat%>:ҫ%>н%{A%}      K "weight: 1.0" ; 

LEXICON N-SUPĂN

%<n%>:                               SUBST ;
%<n%>%<loc%>:%>р%{A%}                K "weight: 1.0" ; 
%<n%>%<abl%>:%>р%{A%}н               K "weight: 1.0" ; 
%<n%>%<px2sg%>%<dat%>:%>н%{A%}       K "weight: 1.0" ; 

LEXICON N-RADIO

%<n%>:%{в%}%{☭%}                     SUBST ;

LEXICON N-TU

%<n%>:%{в%}                          SUBST ;
%<n%>%<px2sg%>%<dat%>:%{в%}%>н%{A%}  K "weight: 1.0" ; 

LEXICON N-ĔŞ 

%<n%>:                               SUBST ;
%<n%>%<px2sg%>%<dat%>:%>н%{A%}       K "weight: 1.0" ; 

LEXICON NP-ZOLOTNICKIJ

%<np%>:%{й%}                         SUBST ;
%<np%>%<nom%>:й                      K "weight: -1.0" ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Nouns 

анне:а                               N-ANNE ;     ! "мать"
атте:а                               N-ATTE ;     ! "отец"
атте-анне:а                          N-ATTEANNE ; ! "родители"

власть:вла                           N-VLAÇ ;     ! "власть"

меню:мен%{й%}ю                       N-RADIO ;    ! "меню"         ! меню́
радио:радио                          N-RADIO ;    ! "радио"        ! ра́дио
кино:кино                            N-RADIO ;    ! "кино"         ! кино́
кенгуру:кенгуру                      N-RADIO ;    ! "кенгуру"      ! кенгуру́

пӳ:пӳ                                N-TU ;       ! ""
ту:ту                                N-TU ;       ! ""
хӑю:хӑ%{й%}ю                         N-TU ;       ! ""

шӑши:шӑши%{й%}                       N-ĔŞ ;       ! ""
ҫурт:ҫурт                            N-ĔŞ ;       ! "дом"

автан:автан                          N-SUPĂN ;    ! ""
автобус:автобус                      N-ĔŞ ;       ! "автобус"
ансамбль:ансамбль                    N-SUPĂN ;    ! "ансамбль"
вӑйӑ:вӑй%{ː%}ӑ                       N ;          ! ""
вӑкӑр:вӑкӑр                          N-ĔŞ ;       ! ""
герой:герой                          N-ĔŞ ;       ! "герой"
информаци:информаци%{й%}             N-ĔŞ ;       ! "информация"
презентаци:презентаци%{й%}           N-ĔŞ ;       ! ""
проект:проект%{ъ%}                   N-ĔŞ ;       ! ""
июль:июль                            N-SUPĂN ;    ! "июль"
июнь:июнь                            N-SUPĂN ;    ! "июнь"
йӗп:йӗп%{ː%}                         N ;          ! ""
каҫма:каҫма                          N ;          ! ""
пахча:пахча                          N ;          ! "сад"
хала:хала                            N ;          ! "город"
класс:класс                          N-ĔŞ ;       ! "класс"
процесс:процесс                      N-ĔŞ ;       ! ""
университет:университет              N-ĔŞ ;       ! ""
контролёр:контролёр                  N-ĔŞ ;       ! "контролёр"
кролик:кролик%{ъ%}                   N-ĔŞ ;       ! "кролик" 
кукӑль:кукӑль                        N-SUPĂN ;    ! ""
кӗнеке:кӗнеке                        N ;          ! "книга"
кӗсье:кӗсье                          N ;          ! ""
кӳлӗ:кӳл%{ː%}ӗ                       N ;          ! ""
лаша:лаша                            N ;          ! ""
май:май                              N-ĔŞ ;       ! "май"
металл:металл                        N-ĔŞ ;       ! "металл"
музей:музей                          N ;          ! "музей"
музей:музей%{ъ%}                     N "weight: 1.0" ; ! "музей"
няня:нян%{й%}я                       N ;          ! ""
проспект:проспект%{ъ%}               N-ĔŞ ;       ! "проспект"
пулӑ:пул%{ː%}ӑ                       N ;          ! ""
пуртӑ:пурт%{ː%}ӑ                     N ;          ! ""
склад:склад                          N-ĔŞ ;       ! "склад"
сарай:сарай                          N-ĔŞ ;       ! ""
район:район                          N-ĔŞ ;       ! "район"
стена:стена                          N ;          ! "стена"
супӑнь:супӑнь                        N-SUPĂN ;    ! ""
сӑвӑ:сӑв%{ː%}ӑ                       N ;          ! ""
тенкӗ:тенкӗ                          N ;          ! "деньги"
тетрадь:тетрадь                      N-ĔŞ ;       ! "тетрадь"
тимӗрҫӗ:тимӗрҫӗ                      N ;          ! ""
уй:уй                                N-ĔŞ ;       ! ""
укҫа:укҫа                            N ;          ! "деньги"
урам:урам                            N-ĔŞ ;       ! "улица"
утӑ:ут%{ː%}ӑ                         N ;          ! ""
хурҫӑ:хурҫӑ                          N ;          ! ""
хӗв:хӗв                              N-ĔŞ ;       ! ""
хӗвел:хӗвел                          N-ĔŞ ;       ! ""
!чей:чей                              N-ĔŞ ;       ! ""
чей:чей                              N ;          ! ""
чикӗ:чик%{ː%}ӗ                       N ;          ! ""
чӗрӗп:чӗрӗп                          N-ĔŞ ;       ! ""
ҫын:ҫын%{ː%}                         N ;          ! "человек"
ӗҫ:ӗҫ                                N-ĔŞ ;       ! "работа"

аллея:алле%{й%}я                     N ;          ! ""
барокко:барокк%{о%}                  N ;          ! "" ! баро́кко
звено:звено                          N ;          ! "" ! звено́
кофе:коф%{е%}                        N ;          ! "" ! ко́фе
литература:литератур%{а%}            N ;          ! "" ! литерату́ра
министерство:министерств%{о%}        N ;          ! "" ! министе́рство
пальто:пальто                        N ;          ! "" ! пальто́
платье:платье                        N ;          ! "" ! пла́тье
!платье:плать%{е%}                    N ;          ! "" 
стихия:стихи%{й%}я                   N ;          ! "" ! стихи́я
тухья:тухь%{й%}я                     N ;          ! "" ! 
училище:училищ%{е%}                  N ;          ! "" ! учи́лище
шоссе:шоссе                          N ;          ! "" ! шоссе́
дата:дата                            N ;          ! 

LEXICON Proper


Женя:Жен%{й%}я                       NP ; ! ""
Золотницкий:Золотницки               NP-ZOLOTNICKIJ ; ! ""
Иванова:Иванов%{а%}                  NP ; ! ""
Наталья:Наталь%{й%}я                 NP ; ! ""
Саша:Саш%{а%}                        NP ; ! ""
Турку:Турку                          NP ; ! ""
Шупашкар:Шупашкар                    NP ; ! ""

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

 LEXICON Punctuation

.%<sent%>:. # ;
..%<sent%>:.. # ;
...%<sent%>:... # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
%–%<guio%>:%– # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%'%<apos%>:%' # ;
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
%\%<sent%>:%\ # ;
%/%<sent%>:%/ # ;

